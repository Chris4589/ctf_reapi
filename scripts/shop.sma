/* 
    Script generated by Pawn Studio 
    
     
    Uso: 
    *************************************************************************
        native ctf_item( const nombre[], precio, adm, descrip[], admt[] );
        forward dar_item( id, item_id ); 
    *************************************************************************
    
    
    
*/

#include <amxmodx>
#include <cstrike>

#define PLUGIN    "Plugin"
#define AUTHOR    "Hypnotize"
#define VERSION    "1.0"

new Array:gName, Array:gPrecio, Array:gDescripcion, Array:gTipo, Array:gTipoTxt, fw_Item_Selected, gTotalItems;

#if AMXX_VERSION_NUM > 182
    #define client_disconnect client_disconnected
#endif

public plugin_init()
{
    register_plugin(PLUGIN, VERSION, AUTHOR);
    // Add your own code here
    fw_Item_Selected = CreateMultiForward("dar_item", ET_STOP, FP_CELL, FP_CELL);
    
    register_clcmd("say /shop", "Items");
    register_clcmd("say /adrenaline", "Items");
    register_clcmd("buyequip", "Items");

}
public plugin_natives()
{
    register_native("ctf_item", "item_register", 0);
}
public plugin_precache()
{
    gName = ArrayCreate(50);
    gDescripcion = ArrayCreate(62);
    gTipoTxt = ArrayCreate(42);
    gPrecio = ArrayCreate();
    gTipo = ArrayCreate();
}

public Items(id)
{
    new szItem[ 90 ], szTipo[ 90 ], admin, precio, menu = menu_create("\wTienda", "GiveItems_Handler");
    new g_isLen[ 400 ];
    for(new i = 0; i < gTotalItems; ++i)
    {
        ArrayGetString(gName, i, szItem, charsmax(szItem) )
        ArrayGetString(gTipoTxt, i, szTipo, charsmax(szTipo) )
        admin = ArrayGetCell( gTipo, i );
        precio = ArrayGetCell( gPrecio, i );
        
        if( admin == ADMIN_ALL )
        {
            if( cs_get_user_money( id ) >= precio )
            {
                formatex(g_isLen, charsmax(g_isLen), "%s | \y[ \r%d \y]", szItem, precio);
            }
            else
            {
                formatex(g_isLen, charsmax(g_isLen), "\d%s | \y[ \r%d \y]", szItem, precio);
            }
        }
        else
        {
            if( get_user_flags(id) & admin )
            {
                if( cs_get_user_money( id ) >= precio )
                {
                    formatex(g_isLen, charsmax(g_isLen), "%s | \y[ \r%d \y]", szItem, precio);
                }
                else
                {
                    formatex(g_isLen, charsmax(g_isLen), "\d%s | \y[ \r%d \y]", szItem, precio);
                }
            }
            else
            {
                formatex(g_isLen, charsmax(g_isLen), "\d%s | ADMIN: \y[ \r%s \y]", szItem, szTipo);
            }
        }
        menu_additem(menu, g_isLen);
    }
    menu_display(id, menu, 0);
    return PLUGIN_HANDLED;
}
public GiveItems_Handler(id, menu, item)
{
    if( item == MENU_EXIT || !(0 <= item < gTotalItems) )
    {
        menu_destroy(menu);
        return PLUGIN_HANDLED;
    }
    new admin = ArrayGetCell( gTipo, item );
    new precio = ArrayGetCell( gPrecio, item );
    
    if( cs_get_user_money( id ) < precio )
    {
        chatcolor(id, "Dinero insuficiente");
        return PLUGIN_HANDLED;
    }
    
    if( admin == ADMIN_ALL )
    {
        GiveItems(id, item);
    }
    else 
    {
        if( get_user_flags(id) & admin )
        {
            GiveItems(id, item);
        }
        else
        {
            chatcolor(id, "Compra un ^4ADMIN^1 para ese ^4ITEM^1");
            Items(id);
        }
    }
    return PLUGIN_HANDLED;
}
public GiveItems(id, item)
{
    new ret;
    ExecuteForward(fw_Item_Selected, ret, id, item);
    
    if ( ret == PLUGIN_HANDLED )
        chatcolor(id, "No puedes comprarlo ahora.");
    else
    {
        new szItemName[32];
        ArrayGetString(gName, item, szItemName, charsmax(szItemName));
        chatcolor(id, "Has comprado: !g%s!y", szItemName);

        cs_set_user_money( id, cs_get_user_money( id ) - ArrayGetCell( gPrecio, item ) );
    }
    return PLUGIN_HANDLED;
}

public item_register(plugin, params)
{
    new szNombre[32]; get_string(1, szNombre, charsmax(szNombre));
    
    ArrayPushString(gName, szNombre);
    ArrayPushCell(gPrecio, get_param(2));
    ArrayPushCell(gTipo, get_param(3));
    
    new szDesp[32]; get_string(4, szDesp, charsmax(szDesp));
    ArrayPushString(gDescripcion, szDesp);
    
    new szTip[32]; get_string(5, szTip, charsmax(szTip));
    ArrayPushString(gTipoTxt, szTip);
    
    ++gTotalItems;

    return gTotalItems-1;
}
stock chatcolor(id, const input[], any:...)
{
    static szMsg[191], msgSayText;
    
    if (!msgSayText)
        msgSayText = get_user_msgid("SayText");
    
    vformat(szMsg, 190, input, 3);
    
    replace_all(szMsg, 190, "!g", "^4");
    replace_all(szMsg, 190, "!y", "^1");
    replace_all(szMsg, 190, "!team", "^3");
    
    message_begin(id ? MSG_ONE_UNRELIABLE : MSG_BROADCAST, msgSayText, .player = id);
    write_byte(id ? id : 33);
    write_string(szMsg);
    message_end();
} 